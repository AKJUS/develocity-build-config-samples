
initscript {
    def pluginVersion = "1.0.0-SNAPSHOT"

    def isTopLevelBuild = gradle.getParent() == null
    if (!isTopLevelBuild) {
        return
    }

    def majorGradleVersion
    try {
        majorGradleVersion = Integer.parseInt(gradleVersion.split("\\.")[0])
    } catch (any) {
        majorGradleVersion = 0
    }

    if (majorGradleVersion >= 5) {
        repositories {
            mavenLocal()
            gradlePluginPortal()
        }
        dependencies {
            classpath("com.myorg:gradle-enterprise-conventions:${pluginVersion}")
        }
    } else if (majorGradleVersion >= 2) {
        repositories {
            mavenLocal()
            maven { url "https://plugins.gradle.org/m2" }
        }
        dependencies {
            classpath("com.myorg:gradle-enterprise-conventions-for-gradle-2-through-4:${pluginVersion}")
        }
    }
}

def isTopLevelBuild = gradle.getParent() == null
if (!isTopLevelBuild) {
    return
}

def majorGradleVersion
try {
    majorGradleVersion = Integer.parseInt(gradleVersion.split("\\.")[0])
} catch (any) {
    any.printStackTrace()
    majorGradleVersion = 0
}

def gradleEnterpriseConventionsPluginClassName = "com.myorg.GradleEnterpriseConventionsPlugin"
def buildScanConventionsPluginClassName = "com.myorg.BuildScansConventionsPlugin"

if (majorGradleVersion >= 6) {
    beforeSettings {
        if (!it.pluginManager.hasPlugin("com.myorg.gradle-enterprise-conventions")) {
            it.pluginManager.apply(initscript.classLoader.loadClass(gradleEnterpriseConventionsPluginClassName))
        }
    }
} else if (majorGradleVersion >= 5) {
    rootProject {
        pluginManager.apply(initscript.classLoader.loadClass(gradleEnterpriseConventionsPluginClassName))
    }
} else if (majorGradleVersion >= 2) {
    rootProject {
        project.apply(initscript.classLoader.loadClass(buildScanConventionsPluginClassName))
    }
}
